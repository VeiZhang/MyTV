/********************* 备注 *****************************/
/**
 * 只需要在根目录下的build.gradle中引入apply from: 'config.gradle'，其他子module则不需要引入config.gradle
 * 或者在根目录下远程引入apply from: "https://github.com/VeiZhang/build.gradle/blob/master/config.gradle?raw=true"
 */

/*********************application通用配置*********************/
def plugins = rootProject.ext.plugins
def cfg = rootProject.ext.android
def libs = rootProject.ext.dependencies

apply plugin: plugins.application

android {
    compileSdkVersion cfg.compileSdkVersion
    defaultConfig {
        // 必须重写
        applicationId "com.excellence.mytv.sample"
        minSdkVersion cfg.minSdkVersion
        targetSdkVersion cfg.targetSdkVersion
        versionCode cfg.versionCode
        versionName cfg.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // 兼容高版本Android Studio
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
    // 兼容高版本Android Studio
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion cfg.buildToolsVersion
                }
            }
        }
    }
    // 去掉AndroidManifest里面Google搜索的警告
    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation libs["appcompat-v7"]
    implementation libs["recyclerview-v7"]
    implementation libs["constraint-layout"]
    implementation project(':MyTVLibrary')
    implementation libs["rxjava2"]
    implementation libs["rxandroid2"]
    implementation libs["annotations"]
    implementation libs["basetools"]
}
